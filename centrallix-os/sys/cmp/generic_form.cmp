$Version=2$
generic_form "widget/component-decl"
    {
    width=640;
    height=480;

    table "widget/parameter" { type=string; default="/samples/Months.csv"; badchars=" '\""; }

    division "widget/parameter"
	{
	type=integer;
	//default=runserver(40 + eval("(select count(1) from " + :this:table + "/columns )")*9);
	default=310;
	}

    cols "widget/parameter" { type=integer; default=3; }

    osrc "widget/osrc"
	{
	sql = runserver("select * from " + :this:table + "/rows");
	baseobj = runserver(:this:table + "/rows");
	replicasize=30;
	readahead=12;
	autoquery = onfirstreveal;

	form "widget/form"
	    {
	    ctls "widget/component"
		{
		x=0;y=0;width=640;height=24;
		path="/sys/cmp/form_controls.cmp";
		bgcolor="#d0d0d0";
		}
	    vbox "widget/vbox"
		{
		x=10;y=40;column_width=runserver(625 / :this:cols - 5);cellsize=20; spacing=5; width=620;

		rpt "widget/repeat"
		    {
		    sql = runserver("select :name, :datatype from " + :this:table + "/columns");
		    field_cmp "widget/component"
			{
			path="/sys/cmp/smart_field.cmp";
			field=runserver(:rpt:name);
			text=runserver(:rpt:name);
			ctl_type=runserver(condition(:rpt:datatype == 'datetime', 'datetime', condition(:rpt:name == 'comments', 'textarea', 'editbox')));

			h1 "widget/hints" { condition=runserver(:rpt:name == 's_date_created'); default=runclient(getdate()); }
			h2 "widget/hints" { condition=runserver(:rpt:name == 's_date_modified'); default=runclient(getdate()); style=alwaysdef; }
			h3 "widget/hints" { condition=runserver(:rpt:name == 's_created_by'); default=runclient(user_name()); }
			h4 "widget/hints" { condition=runserver(:rpt:name == 's_modified_by'); default=runclient(user_name()); style=alwaysdef; }
			}
		    }
		}
	    }

	detail_pane "widget/pane"
	    {
	    x=10;y=runserver(:this:division);width=620;height=runserver(480 - :this:division - 10);
	    style=lowered;

	    detail "widget/table"
		{
		x=0;y=0;width=618;height=runserver(480 - :this:division - 10 - 2);
		mode=dynamicrow;

		row1_bgcolor = "#ffffff";
		row2_bgcolor = "#e0e0e0";
		rowhighlight_bgcolor = "#000080";
		rowheight = 20;
		hdr_bgcolor = "#c0c0c0";
		textcolorhighlight = "#ffffff";
		textcolor = "#000000";

		rpt2 "widget/repeat"
		    {
		    sql = runserver("select :name, :datatype from " + :this:table + "/columns");
		    tbl_field "widget/table-column"
			{
			title = runserver(:rpt2:name);
			fieldname = runserver(:rpt2:name);
			}
		    }
		}
	    }
	}
    }
